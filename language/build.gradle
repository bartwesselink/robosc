buildscript {
	repositories {
		mavenCentral()
		gradlePluginPortal()
	}
	dependencies {
		classpath 'org.xtext:xtext-gradle-plugin:3.0.1'
	}
}

plugins {
  id "com.dorongold.task-tree" version "2.1.0"
}

subprojects {
	ext.xtextVersion = '2.26.0'
	repositories {
		mavenCentral()
	}

	apply plugin: 'java-library'
	dependencies {
		api platform("org.eclipse.xtext:xtext-dev-bom:${xtextVersion}")
	}

	apply plugin: 'org.xtext.xtend'
	apply from: "${rootDir}/gradle/source-layout.gradle"
	apply plugin: 'eclipse'

	group = 'nl.tue.robotsupervisorycontrollerdsl'
	version = '1.0.0-SNAPSHOT'

	sourceCompatibility = '11'
	targetCompatibility = '11'

	configurations.all {
		exclude group: 'asm'
	}
}

// SWT is platform dependent
def swtGroupId = 'org.eclipse.platform'
def swtVersion = '3.119.0'
def swtArtifact = 'org.eclipse.swt.'

def os = System.getProperty("os.name").toLowerCase()
if (os.contains("windows")) { swtArtifact += 'win32.win32.x86_64' }
else if (os.contains("linux")) { swtArtifact += 'gtk.linux.x86_64' }
else if (os.contains("mac")) { swtArtifact += 'cocoa.macosx.x86_64' }

project.ext.swtGroupId = swtGroupId;
project.ext.swtVersion = swtVersion;
project.ext.swtArtifact = swtArtifact;

subprojects {
	configurations.all {
		resolutionStrategy {
			dependencySubstitution {
				substitute module('org.eclipse.platform:org.eclipse.swt.${osgi.platform}') with module("${swtGroupId}:${swtArtifact}:${swtVersion}")
			}
		}
	}
}
